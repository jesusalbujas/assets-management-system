plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'org.adempiere.tools'
version = System.getenv("ADEMPIERE_LIBRARY_VERSION") ?: "local-1.0.0"
def artifactId = 'assets-management-system'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://maven.pkg.github.com/jesusalbujas/assets-management-system"
        credentials {
            username = System.getenv("DEPLOY_USER") ?: System.getProperty("deploy.user")
            password = System.getenv("DEPLOY_TOKEN") ?: System.getProperty("deploy.token")
        }
    }
}

dependencies {
    api fileTree(dir: 'lib', include: ['*.jar'])
    api "io.github.adempiere:base:3.9.4.001"
    implementation 'org.json:json:20210307'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': 'Appwrite Management System',
            'Implementation-Version': version,
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = artifactId
            version = project.version

            pom {
                name = artifactId
                description = 'Fixed Asset Process Management'
                url = 'https://github.com/jesusalbujas/assets-management-system'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jalbujas'
                        name = 'Jes√∫s Albujas'
                        email = 'jesusramirez35000@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/jesusalbujas/assets-management-system")
            credentials {
                username = System.getenv("DEPLOY_USER") ?: System.getProperty("deploy.user")
                password = System.getenv("DEPLOY_TOKEN") ?: System.getProperty("deploy.token")
            }
        }
    }
}
